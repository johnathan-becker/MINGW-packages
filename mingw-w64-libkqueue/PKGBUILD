# Maintainer: John Becker <john.becker@keysight.com>

_realname=libkqueue
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.6.2.r0.g0a750ce
pkgrel=1
pkgdesc="Libkqueue (mingw-w64)"
arch=('any')
mingw_arch=('mingw64' 'ucrt64')
url='https://github.com/mheily/libkqueue'
license=('ISC' 'BSD-2-Clause')
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-clang"
             'git')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
_commit='v2.6.2'
source=("${_realname}"::"git+https://github.com/mheily/libkqueue.git#commit=${_commit}")
sha256sums=('c62ede976fb29f20ed440e03e026f969e25e0b97b4e789f92b35d2282fc7af9f')

pkgver() {
  cd "${srcdir}/${_realname}"

  git describe --long "${_commit}" | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "${srcdir}/${_realname}"

}

build() {

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    CC="clang" \
    CXX="clang++" \
    CFLAGS="-Wno-unused-variable -Wno-unused-const-variable -Wno-unused-function -Wno-unused-value" \
    cmake \
      -G "Unix Makefiles" \
      -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
      -DCMAKE_C_FLAGS="${CFLAGS}" \
      -DCMAKE_BUILD_TYPE=Release \
      -S "${_realname}" \
      -B "build-${MSYSTEM}"

  cmake --build "build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" cmake --install "build-${MSYSTEM}"

  install -Dm644 "${srcdir}/${_realname}/LICENSE" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
